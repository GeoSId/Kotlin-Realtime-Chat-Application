plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
    id 'kotlin-parcelize'
    alias(libs.plugins.google.services)
    alias(libs.plugins.firebase.crashlytics)
    alias(libs.plugins.navigation.safeargs)
}

// Use JVM Toolchain to ensure consistent Java/Kotlin versions
kotlin {
    jvmToolchain(17)
}

if (file("signconfigs.gradle").exists()) {
    apply from: "signconfigs.gradle"
}

android {
    namespace 'com.lkps.ct'
    compileSdkVersion(36)

    defaultConfig {
        applicationId "com.lkps.ct"
        minSdk 28
        targetSdk 36
        versionCode 11
        versionName "1.0.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file("gradle.properties").newDataInputStream())

    buildTypes {
        debug {
            shrinkResources false
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "PUSH_API_K", "\"${properties.getProperty("PUSH_API_KEY")}\""
        }
        release {
            shrinkResources false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "PUSH_API_K", "\"${properties.getProperty("PUSH_API_KEY")}\""
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        String debugDir = 'src/debug/java'
        main.java.srcDirs += debugDir
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        buildConfig true
    }

    testOptions {
        animationsDisabled = true
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    // Kotlin
    implementation platform(libs.kotlin.bom)
    implementation libs.kotlin.stdlib
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.coroutines.play.services

    // AndroidX Core
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.recyclerview
    implementation libs.androidx.cardview
    implementation libs.androidx.swiperefreshlayout
    implementation libs.androidx.viewpager2
    implementation libs.androidx.annotation
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.fragment.ktx

    // Navigation
    implementation libs.androidx.navigation.fragment.ktx
    implementation libs.androidx.navigation.ui.ktx

    // Lifecycle
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.livedata.ktx

    // Firebase (using BOM for version management)
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.auth
    implementation libs.firebase.config
    implementation libs.firebase.firestore
    implementation libs.firebase.storage
    implementation libs.firebase.crashlytics
    implementation libs.firebase.analytics
    implementation libs.firebase.messaging
    implementation libs.firebase.database
    implementation libs.firebase.ui.auth
    implementation libs.guava

    // Networking - Retrofit & OkHttp
    implementation libs.retrofit
    implementation libs.retrofit.adapter.rxjava2
    implementation libs.retrofit.converter.gson
    implementation libs.okhttp
    implementation libs.okhttp.logging.interceptor
    implementation libs.gson

    // Moshi
    implementation libs.moshi
    implementation libs.moshi.kotlin

    // Image Loading - Glide
    implementation libs.glide
    kapt libs.glide.compiler

    // Dependency Injection - Dagger
    implementation libs.dagger
    implementation libs.dagger.android
    implementation libs.dagger.android.support
    kapt libs.dagger.compiler
    kapt libs.dagger.android.processor

    // Dependency Injection - Koin
    implementation libs.koin.android
    implementation libs.koin.android.compat

    // WorkManager
    implementation libs.androidx.work.runtime.ktx
    implementation libs.androidx.work.rxjava2

    // Debug Tools
    implementation libs.timber
}
